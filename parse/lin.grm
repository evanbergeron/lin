structure A = Ast

%%
%header (functor LinLrValsFn (structure Token : TOKEN))

%eop EOF

%term
   EOF
 | ZERO
 | SUCC
 | LPAREN | RPAREN
 | PIPE
 | LAM
 | SARROW
 | COLON
 | NAT
 | APP
 | IDX of int
 | ID of string
 | POLY
 | FST
 | SND
 | COMMA
 | STAR
 | ALL
 | DOT
 | WITH
 | AS
 | IN
 | OF
 | LET
 | EQ
 | IFZ
 | FIX
 | FUN
 | LIN
 | UN

%nonterm
   exp of A.Exp
 | typ of A.Typ
 | qual of A.Qual
 | pretyp of A.PreType
 | program of A.Exp

%verbose
%pos int
%start exp
%eop EOF

%name Lin

(* Each rule is then assigned the precedence of its rightmost terminal *)
(* If the rule has the higher precedence, the reduction is chosen. *)
(* The precedence grows down *)
%nonassoc EOF ZERO SUCC LPAREN RPAREN PIPE LAM COLON NAT IDX ID POLY COMMA STAR FST SND ALL DOT WITH AS IN OF LET EQ IFZ FIX FUN LIN UN
%right SARROW
%nonassoc APP (* Deliberately last *)

%%

exp:
    LPAREN exp RPAREN (exp)
  | exp exp %prec APP (A.App(exp1, exp2))
  | exp typ %prec APP (A.TypApp(typ, exp))
  | ID (A.Var (ID, ~1))
  | qual ZERO (A.Zero qual)
  | SUCC exp (A.Succ exp)
  | LAM ID COLON typ SARROW exp (A.Lam (ID, typ, exp))
  | LET ID COLON typ EQ exp IN exp (A.Let (ID, typ, exp1, exp2))
  | IFZ exp OF ZERO SARROW exp PIPE SUCC ID SARROW exp (A.Ifz (exp1, exp2, ID, exp3))
  | FUN ID COLON typ EQ exp IN exp (A.Let(ID1, typ1, A.Fix(ID1, typ1, exp1), exp2))
  | FIX ID COLON typ IN exp (A.Fix (ID, typ, exp))
  | POLY ID SARROW exp (A.TypAbs (ID, exp))
  | LPAREN exp COMMA exp RPAREN (A.Tuple(exp1, exp2))
  | FST exp (A.ProdLeft exp)
  | SND exp (A.ProdRight exp)

qual:
    LIN (A.Lin)
  | UN (A.Un)
  | (A.Un)

typ:
    LIN pretyp (A.Type(A.Lin, pretyp))
  | pretyp (A.Type(A.Un, pretyp))

pretyp:
    NAT (A.Nat)
  | pretyp SARROW pretyp (A.Arr(pretyp1, pretyp2))
  | pretyp STAR pretyp (A.Prod(pretyp1, pretyp2))
  | LPAREN pretyp RPAREN (pretyp)
  | ID (A.TypVarName ID)
  | ALL ID DOT pretyp (A.All (ID, pretyp))
